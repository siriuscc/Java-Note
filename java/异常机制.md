## 异常原理




[TOC]


之前遇到一个有意思的笔试题目，代码如下，要求给出执行结果。这对于很多人来说是比较困惑的。不妨基于这个问题做一个探讨。


```java {.line-numbers}

 public int finallyNotWork(){
        int temp=100;
        try{
            throw new Exception("a exp");
        }catch (Exception e){
            return temp;
        }finally {
            temp=1;
        }
    }

```
<!-- 方法的返回值为100 -->
如果finally 是在执行完return之后，这时return 的值是被暂存了的；



### 异常的基本流程

> If no such exception handler is found in the current method, the current method invocation completes abruptly (§2.6.5). On abrupt completion, the operand stack and local variables of the current method invocation are discarded, and its frame is popped, reinstating the frame of the invoking method. The exception is then ...
Java 虚拟机标准1.8

首先明确 一个方法对应一个frame，一个线程 就是一个stack，里面很多个frame。方法调用就是frame的入栈。对于异常：

1. 如果当前方法没有 此类异常的handler，当前方法中断；
2. 当中断完成，当前帧的操作数栈，局部变量表 被销毁，此方法对应的frame pop 出栈；
3. 在栈顶方法重新抛出异常；
4. 如果到异常链的顶端依然没能handle住，抛异常的线程终止运行（terminated）；


在编译时已经确定了异常的handler方式。在class 文件中，每个方法的exception handlers 都存在一张表里。运行时，当抛出一个异常，JVM在这个表里搜索异常handler。这个后面详述。




### class文件中的定义：


对于每一个方法，对应的代码有code属性。

```cpp

Code_attribute {
    u2 attribute_name_index;
    u4 attribute_length;
    u2 max_stack;   // 最大的 操作数栈大小
    u2 max_locals;  // 最大本地变量大小
    u4 code_length; //代码长度
    u1 code[code_length];
    u2 exception_table_length;
    { 
        u2 start_pc;    // 异常handler区间 [start_pc, end_pc)
        u2 end_pc;      
        u2 handler_pc;  // 指向异常处理代码开始指令
        u2 catch_type;  // 指向常量池的一个CONSTANT_Class_info，表示catch的类型 
    } exception_table[exception_table_length];
    u2 attributes_count;
    /**
     * 每一个都是一个attribute_info
     *
     */
    attribute_info attributes[attributes_count];
}
```


> end_pc 不包含其实是一个bug。假如是包含的。如果一个方法的代码长度达到最长65535 bytes。最后一个指令时一byte的，那么最后一个指令将无法被handle住。

[TOC]


### Java异常继承体系

+ Throwable
    + Error: 致命异常，程序无法处理，如OutOfMemoryError
    + Exception
        + checked: 受检异常，
        + unchecked: RuntimeException,java.lang.Error, 除零异常，不需要显式catch
            

异常可以由JVM抛出，也可以由Java代码抛出。当JVM遇到严重问题，抛出java.lang.Error 的子类，如 `OOM`，程序一般无法恢复，所以不需要关心此类异常。我们需要关心的是Exception这一族。

Exception 分为：checked和unchecked。对于checked异常，要么显式catch，要么throw出去。而unchecked，诸如NullPointerException,IndexOutOfBoundsException等，这属于代码bug，在运行时由JVM抛出。当然，unchecked 异常也可以catch。


### athrow 指令

在代码中抛出 异常和处理异常 对应 `athrow` 指令 

```java

void cantBeZero(int i) throws TestExc {
    if (i == 0) {
        throw new TestExc();
    }
}
```

```
Method void cantBeZero(int)
 0 iload_1 			// Push argument 1 (i)
 1 ifne 12 			// If i==0, allocate instance and throw
 4 new #1 			// Create instance of TestExc
 7 dup 				// opStack: exc,exc;
 8 invokespecial #7 // 调用 TestExc.<init>()V，opStack:exc;
 11 athrow 			// 抛出 exc；
 12 return 			// 如果抛出了，不会走到这里
```

对于 try-catch:
```java
void catchOne() {
    try {
        tryItOut();
    } catch (TestExc e) {
        handleExc(e);
    }
}
```

```
Method void catchOne()
   0 aload_0 // Beginning of try block
   1 invokevirtual #6 // Method Example.tryItOut()V
   4 return // End of try block; normal return
   5 astore_1 // Store thrown value in local var 1
   6 aload_0 // Push this
   7 aload_1 // Push thrown value
   8 invokevirtual #5 // Invoke handler method: Example.handleExc(LTestExc;)V
   11 return // Return after handling TestExc
  Exception table:
  From To Target Type
   0   4    5    Class TestExc
```




### class文件中的 异常 信息


#### 异常变量表

```
method_info {
    u2 access_flags;
    u2 name_index;
    u2 descriptor_index;
    u2 attributes_count;
    attribute_info attributes[attributes_count];
}
```
对于ClassFile结构，每个 `methodInfo` 有一堆Attributes，其中有一个CodeAttribute结构，里面有一个ExceptionTable存放异常的handler信息。
```
Code_attribute {
    ...
    u2 exception_table_length;
    { 
        u2 start_pc;
        u2 end_pc;
        u2 handler_pc;
        u2 catch_type;
    } exception_table[exception_table_length];
    ...
}
```

如果[start_pc,end_pc) 中产生了异常e， 在异常表 star_pc,end_pc 存在异常E，e是E的子类，则直接跳转到handler_pc。详情后述。


#### 解析 LineNumberTableAttribute

每个methodInfo结构，在CodeAttribute 里有一个LineNumberTableAttribute。记录pc和行号的对应。这个是在打印异常堆栈信息的时候用到的。

```cpp
LineNumberTable_attribute {
    u2 attribute_name_index;
    u4 attribute_length;
    u2 line_number_table_length;
    { 	u2 start_pc;
        u2 line_number;
    } line_number_table[line_number_table_length];
}
```

### 异常的构造

```java
public class RuntimeException extends Exception {

    public RuntimeException() {
        super();
    }
```

抛出异常时，会new一个异常，而所有的异常都继承于`Throwable`。 构造函数的递归调用，最终在Throwable的构造函数中，调用了`fillInStackTrace()`填充堆栈信息。fillInStackTrace 的作用就是把当前线程中，所有的工作frame（除去正在调用的多层异常构造函数对应的frame和调用fillInStackTrace()函数的帧）的信息存储下来。

```java
// 所有的异常都继承于 Throwable
public class Throwable implements Serializable {

    private String detailMessage;

    public Throwable() {
        fillInStackTrace();
    }
    // 填充 栈信息，记录当前thread 的 stack状态，包括 所有的frames
    public synchronized Throwable fillInStackTrace() {
        if (stackTrace != null ||
            backtrace != null /* Out of protocol state */ ) {
            fillInStackTrace(0);
            stackTrace = UNASSIGNED_STACK;
        }
        return this;
    }	

    private native Throwable fillInStackTrace(int dummy);
```


### 打印异常堆栈


如前所述，抛出异常时，先new一个异常对象，构造函数 调用了 `fillInStackTrace（）`填充好堆栈信息。然后，那这个异常对象e去异常handler表里匹配，找到后跳转到对应的指令继续执行。

#### athrow应该打印异常栈

对于 显式抛出的异常，如果没有任何handle，在athrow指令的逻辑里，应该打印异常栈。


稍微总结以下整个流程：

1. 在ClassFile的解析阶段，每个methodInfo 解析出 对应的`LineNumberTableAttribute`和 `ExceptionTableAttribute`
2. 行号对应表和异常handler表是对应到方法上的，运行时的 `Method` 结构应该记录这些信息。在从MethodInfo 解析到Method时做这一步处理；
3. 在异常抛出时，需要根据异常信息，Throwable 构造函数 调用fillInStackTrace(); 填充栈信息。
4. 显式异常抛出时，会调用athrow指令，我们在athrow指令中获取detailMessage （3 中记录的）并打印异常栈。


#### printStackTrace 的逻辑

常见的异常handler方式是：
```java
        try{
            doSomeThing();
        }catch (Exception e){
            e.printStackTrace();
        }
```

如上，常见的handler代码是调用e.printStackTrace()。具体的逻辑如下：

```java
    // Prints this throwable and its backtrace to the standard error stream.
    public void printStackTrace() {
        printStackTrace(System.err);
    }

    public void printStackTrace(PrintStream s) {
        printStackTrace(new WrappedPrintStream(s));
    }

    private void printStackTrace(PrintStreamOrWriter s) {
        // Guard against malicious overrides of Throwable.equals by
        // using a Set with identity equality semantics.
        Set<Throwable> dejaVu =
            Collections.newSetFromMap(new IdentityHashMap<Throwable, Boolean>());
        dejaVu.add(this);

        synchronized (s.lock()) {
            // Print our stack trace
            s.println(this);
            // 获取异常堆栈信息
            StackTraceElement[] trace = getOurStackTrace();
            for (StackTraceElement traceElement : trace)
                s.println("\tat " + traceElement);

            // Print suppressed exceptions, if any
            for (Throwable se : getSuppressed())
                se.printEnclosedStackTrace(s, trace, SUPPRESSED_CAPTION, "\t", dejaVu);

            // Print cause, if any
            Throwable ourCause = getCause();
            if (ourCause != null)
                ourCause.printEnclosedStackTrace(s, trace, CAUSE_CAPTION, "", dejaVu);
        }
    }

    private synchronized StackTraceElement[] getOurStackTrace() {
        // Initialize stack trace field with information from
        // backtrace if this is the first call to this method
        if (stackTrace == UNASSIGNED_STACK ||
            (stackTrace == null && backtrace != null) /* Out of protocol state */) {
            // native ，获取栈深度
            int depth = getStackTraceDepth();
            stackTrace = new StackTraceElement[depth];
            for (int i=0; i < depth; i++)
                // native，获取对应frame 的信息
                stackTrace[i] = getStackTraceElement(i);
        } else if (stackTrace == null) {
            return UNASSIGNED_STACK;
        }
        return stackTrace;
    }
```

这里有两个重要的native方法：getStackTraceDepth 获取栈深度；getStackTraceElement(i) 获取对应的 异常栈信息，返回的是一个StackTraceElement 对象。

`getOurStackTrace` 方法就是 先获取异常信息栈深度，然后逐个获取，组装为数组返回回去，由`printStackTrace` 打印信息



#### 对于JVM抛出的异常，打印异常栈


也就是运行时JVM异常，比如 没有被handle的除零异常，应当打印异常栈。猜测实现思路如下：

1. 创建一个 对应的异常 对象exObj
2. 创建一个 驱动frame，注入代码为 athrow, 先用此帧调用 exObj的初始化方法，然后调用athrow 抛出exObj。
3. 在打印时，区别一下，对于 JVM的驱动栈信息，不要打印。


笔者自己写JVM demo就是这么实现的，感兴趣的可以去hotspot源码中看下[官方实现](http://openjdk.java.net/)。


### try-catch-finally 的实现原理

以反编译一些demo来看Java对于try-catch-finally的实现。

#### 简单 try-catch-finally

```java
    public static void main(String[] args) {

        try {
            divZero();
        } catch (Exception e) {
            System.out.println("catch");
        } finally {
            System.out.println("Finally");
        }
    }
    public static void divZero() {
        int n = 1000;
        System.out.println(n / 0);
    }    
```

反编译后：

```
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=1
try
         0: invokestatic  #2                  // Method divZero:()V
         3: getstatic     #3                  // Field java/lang/System.out:Ljava/io/PrintStream;
try-finally
         6: ldc           #4                  // 加载"Finally" 字符串， 这就是finally块的逻辑
         8: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        11: goto          45                  // 如果正常执行完，goto return
catch        
        14: astore_1                          // catch  保存一下异常对象
        15: getstatic     #3                  // Field java/lang/System.out:Ljava/io/PrintStream;
        18: ldc           #7                  // String catch
catch-finally
        20: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        23: getstatic     #3                  //   catch块嵌入 Finally 块  获取System.out   
        26: ldc           #4                  // String Finally
        28: invokevirtual #5                  // PrintStream.println("finally")
        31: goto          45                  // 到执行终点   
catch-any/finally        
        34: astore_2                          // 保存异常对象
        35: getstatic     #3                  // Field java/lang/System.out:Ljava/io/PrintStream;
        38: ldc           #4                  // String Finally
        40: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        43: aload_2
        44: athrow
方法出口：
        45: return
      Exception table:
         from    to  target type
             0     3    14   Class java/lang/Exception      // [0,3) 的Exception 会goto 14 处理
             0     3    34   any                            // [0,3) 不被Exception catch 的异常  会goto pc=34 处理
            14    23    34   any                            // catch 块如果抛出异常，会跳转到 pc=34 处理，这里pc=34 

```


在异常handler表里， 异常的handle 是从上往下的， 从第0条开始匹配，如果异常是Exception的子类， 跳转到14，如果不是，去查询下一条，type为any表示catch住任意异常，跳转到34，也就是我们说的 catch-any的逻辑。




finally 块的原理已经很清楚了。

+ 对于try块，复制一份 finally块的代码 嵌入到 try块最后一句指令：goto语句之前。如果正常执行完成，goto跳转到return语句
+ 对于catch块，复制一份finally 嵌入到goto语句之前。
+ 对于try块没被catch住的异常，或者是catch块抛出的异常，也称为catch-any, 跳转到 finally块处理。


#### try块有return语句，finally块也有return语句

```java
    public static int returnFinally() {
        try {
            return divZero();
        } catch (Exception e) {
            System.out.println("catch");
            return 1;
        } finally {
            System.out.println("Finally");
            return -1;
        }
    }
```


```
  public static int returnFinally();
    descriptor: ()I
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=0
try:      
         0: invokestatic  #2                  // Method divZero:()I
         3: istore_0                          // 返回值
try-finally: 
         4: getstatic     #3                  // get System.out
         7: ldc           #4                  // String Finally
         9: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        12: iconst_m1                         // opStack.push(-1)
        13: ireturn                           // reutrn -1
catch Exception:
        14: astore_0                          // locals[0]=new Exception() 
        15: getstatic     #3                  // get System.out
        18: ldc           #7                  // String catch
        20: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        23: iconst_1                          // push 1     
        24: istore_1                          // locals[1]=1  返回值暂存到局部变量表
catch-finally
        25: getstatic     #3                  // get System.out
        28: ldc           #4                  // String Finally
        30: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        33: iconst_m1                         // -1 入栈
        34: ireturn                           // return -1
catch-any/finally        
        35: astore_2                          // 异常对象
        36: getstatic     #3                  // get System.out
        39: ldc           #4                  // String Finally
        41: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        44: iconst_m1
        45: ireturn
      Exception table:
         from    to  target type
             0     4    14   Class java/lang/Exception
             0     4    35   any
            14    25    35   any
      LineNumberTable:
        line 18: 0
        line 23: 4
        line 24: 12
        line 19: 14
        line 20: 15
        line 21: 23
        line 23: 25
        line 24: 33
        line 23: 35
        line 24: 44
      StackMapTable: number_of_entries = 2
        frame_type = 78 /* same_locals_1_stack_item */
          stack = [ class java/lang/Exception ]
        frame_type = 84 /* same_locals_1_stack_item */
          stack = [ class java/lang/Throwable ]
```


对于try语句，虽然有return语句，但是 finally块的代码嵌入到 return指令之前。 在try执行完try的代码逻辑，把return值暂存到局部变量表后，执行了finally块，而这时finally块的return指令也嵌入进来了，导致 finally的return指令 在try的return前面，编译器会自动删除后面的无效return（try的）。所以try块的return指令 相当于被finally块的return指令覆盖了。

对于cath块，虽然有return语句，执行完catch的逻辑，把返回值暂存到局部变量表后，就执行嵌入的finally逻辑，还是由于finally块的return 指令在catch块的return 之前，先执行了finally的。而没有看到catch块有两局句return指令，就是编译阶段（javac）删除了无效的return指令。


#### try块有return语句，finally块试图修改返回的变量值


```java
    public static int finallyChange() {
        int n = 0;
        try {
            n = divZero();
            return n;
        } catch (Exception e) {
            System.out.println("catch");
        } finally {
            n = 999;
            System.out.println("Finally");
        }
        return n;
    }
```




```
  public static int finallyChange();
    descriptor: ()I
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=0
         0: iconst_0
         1: istore_0                          // n=0
try:        
         2: invokestatic  #5                  // Method divZero:()I
         5: istore_0                          // locals[0]=divZero() // n
         6: iload_0                           // opStack.push(n)
         7: istore_1                          // locals[1]=n  ， return 值暂存
finally: 8: sipush        999                 // opStack.push(999_
        11: istore_0                          // locals[0]=999
        12: getstatic     #2                  // get System.out
        15: ldc           #6                  // String Finally
        17: invokevirtual #7                  // 打印， java/io/PrintStream.println:(Ljava/lang/String;)V
        20: iload_1                           //  装载locals[1] 到 opStack, returnType =0 
        21: ireturn                           // try 块的 return语句

catch:  22: astore_1                          // locals[1]=exception 对象
        23: getstatic     #2                  //  get System.out
        26: ldc           #9                  // String catch
        28: invokevirtual #7                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
finally:31: sipush        999
        34: istore_0
        35: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        38: ldc           #6                  // String Finally
        40: invokevirtual #7                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        43: goto          61
catch-any:        
        46: astore_2
        47: sipush        999
        50: istore_0
        51: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        54: ldc           #6                  // String Finally
        56: invokevirtual #7                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        59: aload_2                           // 异常
        60: athrow

        61: iload_0
        62: ireturn
      Exception table:
         from    to  target type
             2     8    22   Class java/lang/Exception
             2     8    46   any
            22    31    46   any
      LineNumberTable:
        line 17: 0
        line 19: 2
        line 20: 6
        line 24: 8
        line 25: 12
        line 20: 20
        line 21: 22
        line 22: 23
        line 24: 31
        line 25: 35
        line 26: 43
        line 24: 46
        line 25: 51
        line 27: 61
      StackMapTable: number_of_entries = 3
        frame_type = 255 /* full_frame */
          offset_delta = 22
          locals = [ int ]
          stack = [ class java/lang/Exception ]
        frame_type = 87 /* same_locals_1_stack_item */
          stack = [ class java/lang/Throwable ]
        frame_type = 14 /* same */
```

try 块的语句执行结束后，先把返回值 存到locals[returnAddress] 中暂存，然后执行嵌入的finally逻辑，执行完finally逻辑后执行return指令。也就是说，如果try块有return，finally没有，会在try块的return`指令`前 嵌入finally块的代码。切记，这里是return指令，不是return语句。Java 中return语句可以分解为： 装载局部变量表中的local[returnAddress]到栈顶，执行\<x\>return指令。

catch块没有return语句，首先是将异常对象存到locals[1],执行catch块的逻辑，然后执行嵌入的finally的逻辑，最后goto到 最后，执行`return语句`（装载和返回）的逻辑。 可以这么理解，在没嵌入finally块之前，catch块 的最后一句指令就是goto到 return语句。也就是说 __finally块是嵌入在 catch块的 return指令或 goto指令之前的__。


最后还有一个catch-any块，就是 对于没有被catch住的异常，或者catch块抛出的异常的处理，其实就是finally的逻辑。



#### 多重catch


```java
    public static void multiCatch() {
        try {
            divZero();
        } catch (ArithmeticException e) {
            System.out.println("ArithmeticException catch");
        } catch (Exception e) {
            System.out.println("Exception catch");
        } finally {
            System.out.println("Finally");
        }
    }
```

```
public static void multiCatch();
    descriptor: ()V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=0
try
         0: invokestatic  #3                  // Method divZero:()I
         3: pop
         4: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
         7: ldc           #5                  // String Finally
         9: invokevirtual #6                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        12: goto          66
catch ArithmeticException
        15: astore_0
        16: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
        19: ldc           #10                 // String ArithmeticException catch
        21: invokevirtual #6                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        24: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
        27: ldc           #5                  // String Finally
        29: invokevirtual #6                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        32: goto          66
catch Exception
        35: astore_0
        36: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
        39: ldc           #11                 // String Exception catch
        41: invokevirtual #6                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        44: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
        47: ldc           #5                  // String Finally
        49: invokevirtual #6                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        52: goto          66
catch any
        55: astore_1
        56: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
        59: ldc           #5                  // String Finally
        61: invokevirtual #6                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        64: aload_1
        65: athrow
        66: return
      Exception table:
         from    to  target type
             0     4    15   Class java/lang/ArithmeticException
             0     4    35   Class java/lang/Exception
             0     4    55   any
            15    24    55   any
            35    44    55   any
```

多catch 的实现，就是前面所讲的内容的一个综合，主要利用的还是Exception Table。




### 千万不要在finally里return


```java

    static int a=1;
    static int b=10;
    static int c=100;
    @Test
    public void finallyReturnTest(){

        System.out.println(finallyReturn());
        System.out.println("a:"+a);
        System.out.println("b:"+b);
        System.out.println("c:"+c);
    }
    public static int finallyReturn(){
        try{
            //do something
            return ++a;
        }catch (Exception e){
            return ++b;
        }finally {
            return ++c;
        }
    }



```

基于上面的原理分析，这里在finally 块中写return语句，容易让人困惑。如果没抛异常，a=2,b=10,c=101,路径a-c；如果a抛异常,a=1,b=11,c=101；这里根本的原因是开发者用finally 写了业务逻辑；

一个优秀的开发者产出的所有代码应该是`语义清晰`而`不易被误解`的。正确的使用finally，就是只在finally中做资源的回收。不在finally中写代码还有另一个好处：我们知道finally代码会copy到try块和每个catch块，finally块的指令少，copy的指令就少，运行时 method结构的大小也就小，能节省运行时`内存占用`。


## 后记


之前在牛客上遇到过一次文章开头的这种题目，初次见面，卒；看了解说，下次遇到继续卒；再后来，看了阿里巴巴的 《码出高效》关于这个的解释，还是不甚清晰。 最近，看了JVMS规范，结合自己 对JVM的一些实践，终于完完整整揭开面纱。__技术的理解，应该基于原理，而不是抽象出另一套（表面契合实际上不着边际的）理论去解释__，这种现状在CSDN 等网站泛滥至极，令初学者困惑。




